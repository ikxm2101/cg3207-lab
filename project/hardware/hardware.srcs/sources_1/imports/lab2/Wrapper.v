`timescale 1ns / 1ps
/*
----------------------------------------------------------------------------------
-- Company: NUS	
-- Engineer: (c) Thao Nguyen and Rajesh Panicker  
-- 
-- Create Date: 09/22/2020 06:49:10 PM
-- Module Name: Wrapper
-- Project Name: CG3207 Project
-- Target Devices: Nexys 4 / Basys 3
-- Tool Versions: Vivado 2019.2
-- Description: Wrapper for RISC-V processor. Not meant to be synthesized directly.
-- 
-- Dependencies: NIL
-- 
-- Revision:
-- Revision 0.01 - File Created
-- Additional Comments: The interface SHOULD NOT be modified unless you modify TOP.vhd too. The implementation can be modified.
-- 
----------------------------------------------------------------------------------

----------------------------------------------------------------------------------
--	License terms :
--	You are free to use this code as long as you
--		(i) DO NOT post it on any public repository;
--		(ii) use it only for educational purposes;
--		(iii) accept the responsibility to ensure that your implementation does not violate anyone's intellectual property.
--		(iv) accept that the program is provided "as is" without warranty of any kind or assurance regarding its suitability for any particular purpose;
--		(v) send an email to rajesh<dot>panicker<at>ieee.org briefly mentioning its use (except when used for the course CG3207 at the National University of Singapore);
--		(vi) retain this notice in this file as well as any files derived from this.
----------------------------------------------------------------------------------
*/

//>>>>>>>>>>>> ******* FOR SIMULATION. DO NOT SYNTHESIZE THIS DIRECTLY (This is use as a component in TOP.vhd for Synthesis) ******* <<<<<<<<<<<<

module Wrapper
#(
	parameter N_LEDs_OUT      = 8,   // Number of LEDs displaying Result. LED(15 downto 15-N_LEDs_OUT+1). 8 by default
	parameter N_DIPs = 16,           // Number of DIPs. 16 by default
	parameter N_PBs  = 3             // Number of PushButtons. 3 by default
		                             // [2:0] -> BTNL, BTNC, BTNR. Note that BTNU is used as PAUSE and BTND is used as RESET
)
(
	input  [N_DIPs-1:0] DIP, 		 		// DIP switch inputs. Not debounced. Mapped to 0x00000C04. 
	                                        // Only the least significant 16 bits read from this location are valid. 
	input  [N_PBs-1:0] PB,  				// PB switch inputs. Not debounced.	Mapped to 0x00000C08. 
	                                        // Only the least significant 4 bits read from this location are valid. Order (3 downto 0) -> BTNU, BTNL, BTNR, BTND.
	output reg [N_LEDs_OUT-1:0] LED_OUT, 	// LED(15 downto 8) mapped to 0x00000C00. Only the least significant 8 bits written to this location are used.
	output [6:0] LED_PC, 					// LED(6 downto 0) showing PC(8 downto 2).
	output reg [31:0] SEVENSEGHEX, 			// 7 Seg LED Display. Mapped to 0x00000C18. The 32-bit value will appear as 8 Hex digits on the display.
	output reg [7:0] CONSOLE_OUT,           // CONSOLE (UART) Output. Mapped to 0x00000C0C. The least significant 8 bits written to this location are sent to PC via UART.
											// Check if CONSOLE_OUT_ready (0x00000C14) is set before writing to this location (especially if your CLK_DIV_BITS is small).
											// Consecutive STRs to this location not permitted (there should be at least 1 instruction gap between STRs to this location).
	input	CONSOLE_OUT_ready,				// An indication to the wrapper/processor that it is ok to write to the CONSOLE_OUT (UART hardware).
	                                        // This bit should be set in the testbench to indicate that it is ok to write a new character to CONSOLE_OUT from your program.
	                                        // It can be read from the address 0x00000C14.
	output reg CONSOLE_OUT_valid,           // An indication to the UART hardware that the processor has written a new data byte to be transmitted.
	input  [7:0] CONSOLE_IN,                // CONSOLE (UART) Input. Mapped to 0x00000C0C. The least significant 8 bits read from this location is the character received from PC via UART.
	                                        // Check if CONSOLE_IN_valid flag (0x00000C10) is set before reading from this location.
											// Consecutive LDRs from this location not permitted (needs at least 1 instruction spacing between LDRs).
											// Also, note that there is no Tx FIFO implemented. DO NOT send characters from PC at a rate faster than 
											// your processor (program) can read them. This means sending only 1 char every few seconds if your CLK_DIV_BITS is 26.
											// This is not a problem if your processor runs at a high speed.
	input  	CONSOLE_IN_valid,               // An indication to the wrapper/processor that there is a new data byte waiting to be read from the UART hardware.
	                                        // This bit should be set in the testbench to indicate a new character (Else, the processor will only read in 0x00).
											// It can be read from the address 0x00000C10.
	output reg CONSOLE_IN_ack,              // An indication to the UART hardware that the processor has read the newly received data byte.
	                                        // The testbench should clear CONSOLE_IN_valid when this is set.
	input  RESET,							// Active high. Implemented in TOP as not(CPU_RESET) or Internal_reset (CPU_RESET is red push button and is active low).
	input  CLK								// Divided Clock from TOP.
);                                             

//----------------------------------------------------------------
// RV signals
//----------------------------------------------------------------
wire[31:0] PC ;
wire[31:0] Instr ;
reg[31:0] ReadData ;
wire MemRead ;
wire MemWrite ;
wire[31:0] ALUResult ;
wire[31:0] WriteData ;

//----------------------------------------------------------------
// Address Decode signals
//---------------------------------------------------------------
wire dec_DATA_CONST, dec_DATA_VAR, dec_LED, dec_DIP, dec_CONSOLE, dec_PB, dec_7SEG, dec_CONSOLE_IN_valid, dec_CONSOLE_OUT_ready;  // 'enable' signals from data memory address decoding

//----------------------------------------------------------------
// Memory declaration
//-----------------------------------------------------------------
reg [31:0] INSTR_MEM		[0:127]; // instruction memory
reg [31:0] DATA_CONST_MEM	[0:127]; // data (constant) memory
reg [31:0] DATA_VAR_MEM     [0:127]; // data (variable) memory


//----------------------------------------------------------------
// Instruction Memory
//----------------------------------------------------------------
reg [8:0] i, j;
initial begin
	// TODO: instruction memory goes here. e.g.:INSTR_MEM[0] = 32'hxxxxxxxx;
	
// Instruction Memory Initialization
	INSTR_MEM[0] = 32'h00002497;
	INSTR_MEM[1] = 32'h40048493;
	INSTR_MEM[2] = 32'h00002937;
	INSTR_MEM[3] = 32'h40490913;
	INSTR_MEM[4] = 32'h00002997;
	INSTR_MEM[5] = 32'h3f898993;
	INSTR_MEM[6] = 32'h00002a17;
	INSTR_MEM[7] = 32'h400a0a13;
	INSTR_MEM[8] = 32'h00000a93;
	INSTR_MEM[9] = 32'h00000d13;
	INSTR_MEM[10] = 32'h00800393;
	INSTR_MEM[11] = 32'h00100f93;
	INSTR_MEM[12] = 32'h00000793;
	INSTR_MEM[13] = 32'h00000813;
	INSTR_MEM[14] = 32'h00000893;
	INSTR_MEM[15] = 32'h0009ae83;
	INSTR_MEM[16] = 32'h004ef613;
	INSTR_MEM[17] = 32'h002ef693;
	INSTR_MEM[18] = 32'h001ef713;
	INSTR_MEM[19] = 32'h000e8a63;
	INSTR_MEM[20] = 32'h00002d97;
	INSTR_MEM[21] = 32'hfb4dad83;
	INSTR_MEM[22] = 32'hfffd8d93;
	INSTR_MEM[23] = 32'hfe0d9ee3;
	INSTR_MEM[24] = 32'h00092283;
	INSTR_MEM[25] = 32'h0ff2f313;
	INSTR_MEM[26] = 32'h0072de33;
	INSTR_MEM[27] = 32'h0ffe7e13;
	INSTR_MEM[28] = 32'h006e7b33;
	INSTR_MEM[29] = 32'h006e6bb3;
	INSTR_MEM[30] = 32'h006e0c33;
	INSTR_MEM[31] = 32'h406e0cb3;
	INSTR_MEM[32] = 32'h00069463;
	INSTR_MEM[33] = 32'h0140006f;
	INSTR_MEM[34] = 32'h00078663;
	INSTR_MEM[35] = 32'h00000793;
	INSTR_MEM[36] = 32'h0080006f;
	INSTR_MEM[37] = 32'h00100793;
	INSTR_MEM[38] = 32'h00078463;
	INSTR_MEM[39] = 32'h03c0006f;
	INSTR_MEM[40] = 32'h000a8663;
	INSTR_MEM[41] = 32'hfffa8a93;
	INSTR_MEM[42] = 32'hf95ff06f;
	INSTR_MEM[43] = 32'h00002a97;
	INSTR_MEM[44] = 32'hf54aaa83;
	INSTR_MEM[45] = 32'h000d0a63;
	INSTR_MEM[46] = 32'h0164a023;
	INSTR_MEM[47] = 32'h018a2023;
	INSTR_MEM[48] = 32'h000d7d33;
	INSTR_MEM[49] = 32'hf79ff06f;
	INSTR_MEM[50] = 32'h0174a023;
	INSTR_MEM[51] = 32'h019a2023;
	INSTR_MEM[52] = 32'h00100d13;
	INSTR_MEM[53] = 32'hf69ff06f;
	INSTR_MEM[54] = 32'h01980663;
	INSTR_MEM[55] = 32'h01900833;
	INSTR_MEM[56] = 32'h019008b3;
	INSTR_MEM[57] = 32'h0004a023;
	INSTR_MEM[58] = 32'h011a2023;
	INSTR_MEM[59] = 32'h00061663;
	INSTR_MEM[60] = 32'h00071863;
	INSTR_MEM[61] = 32'hf49ff06f;
	INSTR_MEM[62] = 32'h01f898b3;
	INSTR_MEM[63] = 32'hf41ff06f;
	INSTR_MEM[64] = 32'h41f8d8b3;
	INSTR_MEM[65] = 32'hf39ff06f;
	for (i = 66; i < 128; i = i + 1) begin
		INSTR_MEM[i] = 32'h0;
	end
end

//----------------------------------------------------------------
// Data (Constant) Memory
//----------------------------------------------------------------
initial begin
	// TODO: instruction memory goes here. e.g.:DATA_CONST_MEM[0] = 32'hxxxxxxxx;
	DATA_CONST_MEM[0] = 32'h00000004;
	DATA_CONST_MEM[1] = 32'h00000fff;
	for (i = 2; i < 128; i = i + 1) begin
		DATA_CONST_MEM[i] = 32'h0;
	end
end


//----------------------------------------------------------------
// Data (Variable) Memory
//----------------------------------------------------------------
initial begin
end

//----------------------------------------------------------------
// Debug LEDs
//----------------------------------------------------------------
assign LED_PC = PC[15-N_LEDs_OUT+1 : 2]; // debug showing PC

//----------------------------------------------------------------
// RV port map
//----------------------------------------------------------------
RV IRV1(
	.CLK(CLK),
	.RESET(RESET),
	.Instr(Instr),
	.ReadData(ReadData),
	.MemRead(MemRead),
	.MemWrite(MemWrite),
	.PC(PC),
	.ALUResult(ALUResult),
	.WriteData(WriteData)
);

//----------------------------------------------------------------
// Data memory address decoding
//----------------------------------------------------------------
assign dec_DATA_CONST		= (ALUResult >= 32'h00002000 && ALUResult <= 32'h000021FC) ? 1'b1 : 1'b0;
assign dec_DATA_VAR			= (ALUResult >= 32'h00002200 && ALUResult <= 32'h000023FC) ? 1'b1 : 1'b0;
assign dec_LED				= (ALUResult == 32'h00002400) ? 1'b1 : 1'b0;
assign dec_DIP				= (ALUResult == 32'h00002404) ? 1'b1 : 1'b0;
assign dec_PB 		   		= (ALUResult == 32'h00002408) ? 1'b1 : 1'b0;
assign dec_CONSOLE	   		= (ALUResult == 32'h0000240C) ? 1'b1 : 1'b0;
assign dec_CONSOLE_IN_valid	= (ALUResult == 32'h00002410) ? 1'b1 : 1'b0;
assign dec_CONSOLE_OUT_ready= (ALUResult == 32'h00002414) ? 1'b1 : 1'b0;
assign dec_7SEG	    		= (ALUResult == 32'h00002418) ? 1'b1 : 1'b0;

//----------------------------------------------------------------
// Data memory read
//----------------------------------------------------------------
always@( * ) begin
if (dec_DIP)
	ReadData <= { {31-N_DIPs+1{1'b0}}, DIP } ; 
else if (dec_PB)
	ReadData <= { {31-N_PBs+1{1'b0}}, PB } ; 
else if (dec_DATA_VAR)
	ReadData <= DATA_VAR_MEM[ALUResult[8:2]] ; 
else if (dec_DATA_CONST)
	ReadData <= DATA_CONST_MEM[ALUResult[8:2]] ; 
else if (dec_CONSOLE && CONSOLE_IN_valid)
	ReadData <= {24'b0, CONSOLE_IN};
else if (dec_CONSOLE_IN_valid)
	ReadData <= {31'b0, CONSOLE_IN_valid};	
else if (dec_CONSOLE_OUT_ready)
	ReadData <= {31'b0, CONSOLE_OUT_ready};		
else
	ReadData <= 32'h0 ; 
end
			
//----------------------------------------------------------------
// Instruction memory read
//----------------------------------------------------------------
assign Instr = ( (PC >= 32'h00000000) && (PC <= 32'h000001FC) ) ? // To check if address is in the valid range, assuming 128 word memory. Also helps minimize warnings
                 INSTR_MEM[PC[8:2]] : 32'h00000000 ; 

//----------------------------------------------------------------
// Console read / write
//----------------------------------------------------------------
always @(posedge CLK) begin
	CONSOLE_OUT_valid <= 1'b0;
	CONSOLE_IN_ack <= 1'b0;
	if (MemWrite && dec_CONSOLE && CONSOLE_OUT_ready)
	begin
		CONSOLE_OUT <= WriteData[7:0];
		CONSOLE_OUT_valid <= 1'b1;
	end
	if (MemRead && dec_CONSOLE && CONSOLE_IN_valid)
		CONSOLE_IN_ack <= 1'b1;
end
// Possible spurious CONSOLE_IN_ack and a lost character if we don't have a MemRead signal. ALternatively, make sure ALUResult is never the address of UART other than when accessing it.
// Also, the character received from PC in the CLK cycle immediately following a character read by the processor is lost. This is not that much of a problem in practice though.

//----------------------------------------------------------------
// Data Memory-mapped LED write
//----------------------------------------------------------------
always@(posedge CLK) begin
    if(RESET)
        LED_OUT <= 0 ;
    else if( MemWrite && dec_LED ) 
        LED_OUT <= WriteData[N_LEDs_OUT-1 : 0] ;
end

//----------------------------------------------------------------
// SevenSeg LED Display write
//----------------------------------------------------------------
always @(posedge CLK) begin
	if (RESET)
		SEVENSEGHEX <= 32'b0;
	else if (MemWrite && dec_7SEG)
		SEVENSEGHEX <= WriteData;
end

//----------------------------------------------------------------
// Data Memory write
//----------------------------------------------------------------
always@(posedge CLK) begin
    if( MemWrite && dec_DATA_VAR ) 
        DATA_VAR_MEM[ALUResult[8:2]] <= WriteData ;
end

endmodule
